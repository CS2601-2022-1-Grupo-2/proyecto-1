# Maintainer: Otreblan <otreblain@gmail.com>

pkgname=proyecto-1-git
pkgver=r6.1a6b406
pkgrel=1
pkgdesc="SVM"
arch=('any')
url="https://github.com/CS2601-2022-1-Grupo-2/proyecto-1"
license=('GPL')
depends=(
	"python-numpy"
	"python-pandas"
	"python-pywavelets"
)
makedepends=(
	"python-build"
	"python-installer"
	"python-wheel"
)
checkdepends=()
optdepends=()
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
_gitrepo=$(git rev-parse --show-toplevel)
_headcommit=$(git rev-parse HEAD)
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cd "$srcdir/$pkgname"

	python -m build --wheel --no-isolation
}

package() {
	cd "$srcdir/$pkgname"

	python -m installer --destdir="$pkgdir" dist/*.whl
}
